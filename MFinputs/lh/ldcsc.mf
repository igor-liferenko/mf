font_coding_scheme:=if ligs=0: "TeX typewriter text"
 else: "TeX text without f-ligs" fi;
   spanish_shriek=oct"016"; spanish_query=oct"017";

input lxpseudo;

mode_setup; font_setup;
pseudo_setup;    % |pseudo_setup| from ec

 input romanu;      % upper case (majuscules)
 input greeku;      % upper case greek letters
 input romand;      % numerals
 input romanp;      % ampersand, question marks, currency sign
 input romspu;      % uppercase specials (\AE, \OE, \O)
 input punct;       % punctuation symbols common to all roman and italic
 input accent;            % accents common to roman and italic text
 input romsub;            % substitutes for ligatures

   if ligs>0: input comlig; fi  % ligatures common with italic

input lgrusu;            % input uppercase shapes
input lgcyrsym;            % <<, >>, No


font_slant slant; font_x_height x_height#;

upper.u#:=u#; %save for Cyrillic kerning table

if monospace: font_normal_space 9u#; % no stretching or shrinking
   font_quad 18u#;
   font_extra_space 9u#;
   ligtable "!": "`" =: spanish_shriek;
   ligtable "?": "`" =: spanish_query;
else: font_normal_space 6u#+2letter_fit#;
   font_normal_stretch 3u#; font_normal_shrink 2u#;
   font_quad 18u#+4letter_fit#;
   font_extra_space 2u#;
   k#:=-.5u#; kk#:=-1.5u#; kkk#:=-2u#; % three degrees of kerning
   %
   % Uppercase Latin ligs & kerns
   %   There are just the same ligs and kerns as in CM
   %   but they are built in more visible form
   %
   boolean TITLEfont,SmallCapsfont,ROMANfont;
   TITLEfont:=false; SmallCapsfont:=true; ROMANfont:=false;
      ligtable "P": "T": "Y":
                    "a" kern kk#,
                    "A" kern kk#;
      ligtable "F": "V": "W":
                    if serifs:  "a" kern kkk#,
                                "A" kern kkk#,
                         else:  "a" kern kk#,
                                "A" kern kk#, fi
               "K": "X":
                    "o" kern k#,
                    "O" kern k#,
                    "c" kern k#,
                    "C" kern k#,
                    "g" kern k#,
                    "G" kern k#,
                    "q" kern k#,
                    "Q" kern k#;
      ligtable "O": "D":
                    "x" kern k#,
                    "X" kern k#,
                    "w" kern k#,
                    "W" kern k#,
                    "a" kern k#,
                    "A" kern k#,
                    "v" kern k#,
                    "V" kern k#,
                    "y" kern k#,
                    "Y" kern k#;
      ligtable "A": if serifs: "R": fi
                    "c" kern k#,
                    "C" kern k#,
                    "o" kern k#,
                    "O" kern k#,
                    "g" kern k#,
                    "G" kern k#,
                    "u" kern k#,
                    "U" kern k#,
                    "q" kern k#,
                    "Q" kern k#,
               "L": "T" kern kk#,
                    "t" kern kk#,
                    "Y" kern kk#,
                    "y" kern kk#,
                    "V" kern kkk#,
                    "v" kern kkk#,
                    "W" kern kkk#,
                    "w" kern kkk#;
      ligtable "I": "I" kern -k#; %fi % Richard III
      %
      %% ligature programs for |"-"|, |"`"|, and |"'"| are in the {\tt comlig} file
      ligtable "!": "`" =: spanish_shriek;
      ligtable "?": "`" =: spanish_query;
      ligtable CYR_.frqq: "." kern kk#, "," kern kk#;
   % uppercase Cyrillic ligs & kerns moved down
fi

%%% fine lower
forsuffixes $= u, width_adj, cap_serif_fit, letter_fit,
   body_height, cap_height, x_height, bar_height, comma_depth,
   flare, cap_hair, stem, cap_stem, cap_curve, cap_ess,
   cap_jut, beak_jut, beak, slab, cap_bar, cap_band, cap_notch_cut,
   o, apex_o:  $.#:=lower.$.#; endfor
fudge:=lower.fudge;

if is_small_cap:
   cap_sbeak#          := 7/8 if serifs: max(beak#,desc_depth#) else: desc_depth# fi;
   define_pixels(cap_sbeak);
fi

% now try again with |lower| settings
font_setup; pseudo_setup; % |pseudo_setup| from ec

   extra_endchar:=extra_endchar&"charcode:=charcode+code_offset";
   code_offset:=ASCII"a" - ASCII"A";
   input romanu;  % majuscules (in lowercase positions)
   code_offset:=-3;
   input romspu;  % \AE, \OE, \O\ (in positions of \ae, \oe, \o)
   code_offset:=0;
   input cscspu;  % substitutes for the remaining characters of romspl

is_small_cap:=true;
code_offset:=CYR_.a - CYR_.A;
%%% fine higher
% the `higher' values are used while putting accents
forsuffixes $=u,o,letter_fit,x_height,bar_height,body_height,stem:
  higher.$.#:=$.#; higher.$:=$;
endfor
input lgrusu;  % Cyrillic majuscules (in lowercase positions)


if not monospace:
   k#:=-.5u#; kk#:=-1.5u#; kkk#:=-2u#; % three degrees of kerning
   %
   % Lowercase Latin ligs & kerns
   %   There are just the same ligs and kerns as in CM
   %   but they are built in more visible form
   %
      ligtable "p": "t": "y":
                    "a" kern kk#;
      ligtable "f": "v": "w":
                    "a" kern if serifs: kkk# else: kk#\\fi,
               "k": "x":
                    "o" kern k#,
                    "c" kern k#,
                    "g" kern k#,
                    "q" kern k#;
      ligtable "o": "d":
                    "x" kern k#,
                    "w" kern k#,
                    "a" kern k#,
                    "v" kern k#,
                    "y" kern k#;
      ligtable "a": if serifs: "r": fi
                    "c" kern k#,
                    "o" kern k#,
                    "g" kern k#,
                    "u" kern k#,
                    "q" kern k#,
               "l": "'" kern kk#,
                    "t" kern kk#,
                    "y" kern kk#,
                    "v" kern kkk#,
                    "w" kern kkk#;
      ligtable "i": "i" kern -k#;  % skiing
   %
   u#:=upper.u#;     % restored start u parameter
   k#:=-.5u#; kk#:=-1.5u#; kkk#:=-2u#; % repeat three degrees of kerning
   input omliker;
fi

bye.
%end of file
